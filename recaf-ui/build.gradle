plugins {
    id 'application'
    alias(libs.plugins.javafx)
    alias(libs.plugins.shadow)
}

def javaFxIncludeInDist = System.getProperty('skip.jfx.bundle') == null

javafx {
    version = libs.versions.javafx.lib.get()
    modules = ['javafx.controls', 'javafx.media']
}

dependencies {
    implementation project(':recaf-core')
    testImplementation(testFixtures(project(":recaf-core")))

    implementation(libs.atlantafx)
    implementation(libs.docking)
    implementation(libs.bundles.ikonli)
    implementation(libs.jsvg)
    implementation(libs.reactfx)
    implementation(libs.richtextfx)
    implementation(libs.treemapfx)
}

application {
    mainClass = 'software.coley.recaf.Main'
}

shadowJar {
    exclude "META-INF/maven/**"
    exclude "META-INF/rewrite/**"
    exclude "META-INF/proguard/*"
    exclude "META-INF/native-image/*"
    exclude "META-INF/*.properties"

    dependencies {
        exclude(dependency(javaFxIncludeInDist ? 'invalid:invalid:invalid' : 'org.openjfx:.*:.*'))
    }
}